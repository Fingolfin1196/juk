#MIN_CONFIG(3)

AM_INIT_AUTOMAKE(juk,1.0)

AC_PATH_PROG(ARTSC_CONFIG, artsc-config, no)

if test "x$build_arts" = "xyes" && test "x$ARTSC_CONFIG" != "xno" ; then
	LIB_ARTS="-lartskde"
	ARTS_PREFIX=[`$ARTSC_CONFIG --arts-prefix`]
	ARTS_CFLAGS="-I$ARTS_PREFIX/include/arts"
	AC_DEFINE(HAVE_ARTS, 1, [have arts support in juk])
else
	build_arts="no"
	LIB_ARTS=""
	ARTS_CFLAGS=""
	AC_DEFINE(HAVE_ARTS, 0, [no arts support in juk])
fi

AC_SUBST(LIB_ARTS)
AC_SUBST(ARTS_CFLAGS)

KDE_CHECK_HEADER(tunepimp/tp_c.h,
[
	AC_DEFINE(HAVE_MUSICBRAINZ, 1, [have MusicBrainz])
	have_musicbrainz=true
], [
	AC_DEFINE(HAVE_MUSICBRAINZ, 0, [have MusicBrainz])
	have_musicbrainz=false
])

AM_CONDITIONAL(link_lib_MB, test x$have_musicbrainz = xtrue)

# pkg-config seems to have a bug where it masks needed -L entries when it
# shouldn't, so disable that.

PKG_CONFIG_ALLOW_SYSTEM_LIBS=1
export PKG_CONFIG_ALLOW_SYSTEM_LIBS

dnl start with 0.8
GST_MAJORMINOR=0.8
GST_REQ=0.8.0

PKG_CHECK_MODULES(GST, \
	gstreamer-$GST_MAJORMINOR >= $GST_REQ \
	gstreamer-control-$GST_MAJORMINOR >= $GST_REQ \
	gstreamer-libs-$GST_MAJORMINOR >= $GST_REQ,
	have_gst=yes, have_gst=no)

dnl Give error if we don't have gstreamer
if test "x$have_gst" = "xno"; then
	CFLAGS_GST=""
	LDADD_GST=""
	LDFLAGS_GST=""
	AC_MSG_WARN([GStreamer version >= $GST_REQ required.])
	AC_DEFINE(HAVE_GSTREAMER, 0, [have GStreamer])
else
	CFLAGS_GST=$GST_CFLAGS
	LDADD_GST=`$PKG_CONFIG --libs-only-l gstreamer-$GST_MAJORMINOR`
	LDFLAGS_GST=`$PKG_CONFIG --libs-only-other gstreamer-$GST_MAJORMINOR`

	# Append -L entries, since they are masked by --libs-only-l and
	# --libs-only-other
	LIBDIRS_GST=`$PKG_CONFIG --libs-only-L gstreamer-$GST_MAJORMINOR`
	LDADD_GST="$LDADD_GST $LIBDIRS_GST"

	AC_SUBST(CFLAGS_GST)
	AC_SUBST(LDADD_GST)
	AC_SUBST(LDFLAGS_GST)
	AC_MSG_NOTICE([GStreamer version >= $GST_REQ found.])
	AC_DEFINE(HAVE_GSTREAMER, 1, [have GStreamer])
fi

if test "x$have_taglib" = "xfalse" || ( test "x$build_arts" = "xno" && test "x$have_gst" = "xno" ) ; then
	DO_NOT_COMPILE="$DO_NOT_COMPILE juk"
fi
